\begin{code}
{-# OPTIONS --safe --without-K --postfix-projections #-}
module Generic.Simple.Environment (Ty : Set) where

  open import Data.LTree
  open import Data.LTree.Vector
  open import Function.Base using (_∘_)
  open import Function.Extra using ([_]_⊨_)
  open import Level
  open import Relation.Binary.PropositionalEquality
  open import Relation.Unary

  open import Generic.Simple.Syntax Ty

  private
    variable
      𝓥 : OpenFam
      T : Ctx → Set
      A : Ty
      Γ Δ Θ : Ctx

  infix 20 _∋_ _⇒ʳ_

  record _∋_ (Γ : Ctx) (A : Ty) : Set where
    constructor el
    field
      idx : Ptr (Γ .shape)
      tyq : Γ .ty-ctx idx ≡ A
  open _∋_ public

  [_]_⇒ᵉ_ : OpenFam → (Γ Δ : Ctx) → Set
  [ 𝓥 ] Γ ⇒ᵉ Δ = ∀ {A} (v : Δ ∋ A) → 𝓥 Γ A

  -- record [_]_⇒ᵉ_ (𝓥 : OpenFam) (Γ Δ : Ctx) : Set where
  --   constructor pack
  --   field lookup : ∀ {A} (v : Δ ∋ A) → 𝓥 Γ A
  -- open [_]_⇒ᵉ_ public

  leftᵛ : ∀ {s t A Γ} → ctx (Γ ∘ ↙) ∋ A → ctx {s <+> t} Γ ∋ A
  leftᵛ (el i q) = el (↙ i) q
  rightᵛ : ∀ {s t A Γ} → ctx (Γ ∘ ↘) ∋ A → ctx {s <+> t} Γ ∋ A
  rightᵛ (el i q) = el (↘ i) q

  _⇒ʳ_ : (Γ Δ : Ctx) → Set
  _⇒ʳ_ = [ _∋_ ]_⇒ᵉ_

  □ : (Ctx → Set) → (Ctx → Set)
  (□ T) Γ = ∀[ (_⇒ʳ Γ) ⇒ T ]

  Renameable : (Ctx → Set) → Set
  Renameable T = ∀[ T ⇒ □ T ]

  ren^Var : Renameable (_∋ A)
  ren^Var v th = th v

  identity : Γ ⇒ʳ Γ
  identity v = v

  select : Γ ⇒ʳ Δ → [ 𝓥 ] Θ ⇒ᵉ Γ → [ 𝓥 ] Θ ⇒ᵉ Δ
  select th ρ v = ρ (th v)

  extract : ∀[ □ T ⇒ T ]
  extract t = t identity

  duplicate : ∀[ □ T ⇒ □ (□ T) ]
  duplicate t th ph = t (select {𝓥 = _∋_} th ph)

  ren^□ : Renameable (□ T)
  ren^□ = duplicate

  ren^Env : (∀ {A} → Renameable ([ 𝓥 ]_⊨ A)) → Renameable ([ 𝓥 ]_⇒ᵉ Γ)
  ren^Env th^𝓥 ρ th v = th^𝓥 (ρ v) th
\end{code}
