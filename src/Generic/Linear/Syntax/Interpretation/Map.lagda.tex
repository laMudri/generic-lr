\begin{code}
{-# OPTIONS --safe --without-K --postfix-projections #-}

open import Algebra.Po
open import Level using (Level; 0ℓ)

module Generic.Linear.Syntax.Interpretation.Map
  (Ty : Set) (poSemiring : PoSemiring 0ℓ 0ℓ 0ℓ)
  where

  open import Algebra.Po.Relation
  open import Algebra.Po.Construct.Vector
  open import Algebra.Relational
  open import Algebra.Relational.Relation
  open import Data.Bool.Extra
  open import Data.Unit.Polymorphic
  open import Data.Product
  open import Data.LTree
  open import Data.LTree.Vector
  open import Function.Base
  open import Function.Extra
  open import Relation.Binary.PropositionalEquality as ≡
  open import Relation.Nary.Extra using (_⊆_)
  -- open import Relation.Unary.Checked using (_⊆_)

  open PoSemiring poSemiring renaming (Carrier to Ann)

  open import Generic.Linear.Operations rawPoSemiring
  open import Generic.Linear.Algebra poSemiring
  open import Generic.Linear.Syntax Ty Ann
  open import Generic.Linear.Syntax.Interpretation Ty rawPoSemiring

  private
    variable
      x y : Level

  module _ {ℓ s t} {γ : Vector Ty s} {δ : Vector Ty t}
           (F : LinFuncRel s t ℓ) {X : ExtOpenFam x} {Y : ExtOpenFam y}
    where

    private open module Dummy {s} = FRelLeftSemimodule (Vᶠᴿ s)
\end{code}

%<*map-p>
\begin{code}
    map-p : (ps : Premises) →
      (∀ {Θ P′ Q′} → F .rel P′ Q′ → X Θ (ctx P′ γ) ⊆ Y Θ (ctx Q′ δ)) →
      (∀ {P Q} → F .rel P Q → ⟦ ps ⟧p X (ctx P γ) → ⟦ ps ⟧p Y (ctx Q δ))
    map-p ⟨ Γ `⊢ A ⟩ f r M                = f r M
    map-p `⊤ f r _                        = _
    map-p (ps `∧ qs) f r (M , N)          = map-p ps f r M , map-p qs f r N
    map-p `ℑ f r ℑᶜ⟨ sp0 ⟩                = ℑᶜ⟨ F .rel-0ₘ (sp0 , r) ⟩
    map-p (ps `✴ qs) f r (M ✴ᶜ⟨ sp+ ⟩ N)  =
      let rM ↘, sp+′ ,↙ rN = F .rel-+ₘ (sp+ , r) in
      map-p ps f rM M ✴ᶜ⟨ sp+′ ⟩ map-p qs f rN N
    map-p (p `· ps) f r (⟨ sp* ⟩·ᶜ M)     =
      let r′ , sp*′ = F .rel-*ₘ (sp* , r) in
      ⟨ sp*′ ⟩·ᶜ map-p ps f r′ M
\end{code}
%</map-p>

\begin{code}
    map-r : (r : Rule) →
      (∀ {Θ P′ Q′} → F .rel P′ Q′ → X Θ (ctx P′ γ) ⊆ Y Θ (ctx Q′ δ)) →
      (∀ {P Q} → F .rel P Q → ⟦ r ⟧r X (ctx P γ) ⊆ ⟦ r ⟧r Y (ctx Q δ))
    map-r (ps =⇒ A) f r (q , M) = q , map-p ps f r M
\end{code}

%<*map-s-type>
\begin{code}
    map-s : (s : System) →
      (∀ {Θ P′ Q′} → F .rel P′ Q′ → X Θ (ctx P′ γ) ⊆ Y Θ (ctx Q′ δ)) →
      (∀ {P Q} → F .rel P Q → ⟦ s ⟧s X (ctx P γ) ⊆ ⟦ s ⟧s Y (ctx Q δ))
\end{code}
%</map-s-type>
\begin{code}
    map-s (L ▹ rs) f r (l , M) = l , map-r (rs l) f r M

  module _ {X : ExtOpenFam x} {Y : ExtOpenFam y} where

    map-p′ : (ps : Premises) → X ⊆ Y → ⟦ ps ⟧p X ⊆ ⟦ ps ⟧p Y
    map-p′ ⟨ Γ `⊢ A ⟩ f t = f t
    map-p′ `⊤ f _ = _
    map-p′ `ℑ f ℑᶜ⟨ sp ⟩ = ℑᶜ⟨ sp ⟩
    map-p′ (ps `∧ qs) f (s , t) = map-p′ ps f s , map-p′ qs f t
    map-p′ (ps `✴ qs) f (s ✴ᶜ⟨ sp ⟩ t) =
      map-p′ ps f s ✴ᶜ⟨ sp ⟩ map-p′ qs f t
    map-p′ (r `· ps) f (⟨ sp ⟩·ᶜ t) = ⟨ sp ⟩·ᶜ map-p′ ps f t

    map-r′ : (r : Rule) → X ⊆ Y → ⟦ r ⟧r X ⊆ ⟦ r ⟧r Y
    map-r′ (ps =⇒ A) f (q , t) = q , map-p′ ps f t
\end{code}

%<*map-s-prime-type>
\begin{code}
    map-s′ : (s : System) → X ⊆ Y → ⟦ s ⟧s X ⊆ ⟦ s ⟧s Y
\end{code}
%</map-s-prime-type>
\begin{code}
    map-s′ (L ▹ rs) f (l , t) = l , map-r′ (rs l) f t

  open import Category.Applicative

  module _ {F : Set x → Set x} {{appF : RawApplicative F}} where

    open RawApplicative appF

    sequence-p :
      ∀ {X : ExtOpenFam x} (ps : Premises) →
      ⟦ ps ⟧p (λ Δ Γ B → F (X Δ Γ B)) ⊆ F ∘ ⟦ ps ⟧p X
    sequence-p (⟨ Γ `⊢ A ⟩) t = t
    sequence-p `⊤ tt = pure tt
    sequence-p `ℑ t = pure t
    sequence-p (ps `∧ qs) (s , t) = _,_ <$> sequence-p ps s ⊛ sequence-p qs t
    sequence-p (ps `✴ qs) (s ✴ᶜ⟨ sp ⟩ t) =
      _✴ᶜ⟨ sp ⟩_ <$> sequence-p ps s ⊛ sequence-p qs t
    sequence-p (ρ `· ps) (⟨ sp ⟩·ᶜ t) =
      ⟨ sp ⟩·ᶜ_ <$> sequence-p ps t

    sequence-r :
      ∀ {X : ExtOpenFam x} (r : Rule) → ∀ {A} →
      _⟨ ⟦ r ⟧r (λ Δ Γ B → F (X Δ Γ B)) ⟩⊢ A ⊆ F ∘ _⟨ ⟦ r ⟧r X ⟩⊢ A
    sequence-r (ps =⇒ A) (q , t) = (q ,_) <$> sequence-p ps t

    sequence-s :
      ∀ {X : ExtOpenFam x} (s : System) → ∀ {A} →
      _⟨ ⟦ s ⟧s (λ Δ Γ B → F (X Δ Γ B)) ⟩⊢ A ⊆ F ∘ _⟨ ⟦ s ⟧s X ⟩⊢ A
    sequence-s (L ▹ rs) (l , t) =
      (l ,_) <$> sequence-r (rs l) t
\end{code}
