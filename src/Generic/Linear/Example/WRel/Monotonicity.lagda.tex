\begin{code}
{-# OPTIONS --sized-types --without-K --postfix-projections --prop #-}

module Generic.Linear.Example.WRel.Monotonicity where

  open import Algebra.Po
  open import Algebra.Relational
  open import Data.LTree
  open import Data.LTree.Vector
  open import Data.Product as ×
  open import Data.Unit using (⊤; tt)
  open import Function.Base as F using (id; _∘_; _$_)
  open import Level
  open import Relation.Binary using (Rel; Poset)
  open import Relation.Binary.PropositionalEquality as ≡ using (_≡_)
  open import Relation.Unary.Bunched.Synth
  open import Relation.Unary using (U)
  open import Relation.Nary
  open import Size

  open import Generic.Linear.Example.WRel
  open import Generic.Linear.Example.Mono

  module WithPoset (poset : Poset 0ℓ 0ℓ 0ℓ) where
    open Poset poset renaming (_≤_ to _≤ˢ_)

    open WithPoSemiring poSemiring public

    0CRM : CommutativeRelMonoid 0ℓ 0ℓ
    0CRM = record
      { Carrier = ⊤
      ; _≤_ = λ _ _ → ⊤
      ; _≤ε = λ _ → ⊤
      ; _≤[_∙_] = λ _ _ _ → ⊤
      ; isProset = _
      ; ε-mono = _
      ; ∙-mono = _
      ; isCommutativeRelMonoid = _
      }
    open WithWorlds 0CRM
\end{code}

%<*BaseR>
\begin{code}
    Baseᴿ : WRel _≤ʷ_
    Baseᴿ .set = Carrier
    Baseᴿ .rel x y _ = x ≤ˢ y
    Baseᴿ .subres _ = id
\end{code}
%</BaseR>

%<*BangR>
\begin{code}
    !ᴿ : WayUp → WRel _≤ʷ_ → WRel _≤ʷ_
    !ᴿ a R .set = R .set
    !ᴿ ↑↑ R .rel = R .rel
    !ᴿ ↓↓ R .rel x y = R .rel y x
    !ᴿ ?? R .rel x y = U
    !ᴿ ~~ R .rel x y = R .rel x y ∩ R .rel y x
    !ᴿ a R .subres _ = id
\end{code}
%</BangR>

\begin{code}
    !ᴿ-map : ∀ {a} {R S : WRel _≤ʷ_} → WRelMor R S → WRelMor (!ᴿ a R) (!ᴿ a S)
    !ᴿ-map f .set⇒ = f .set⇒
    !ᴿ-map {↑↑} f .rel⇒ = f .rel⇒
    !ᴿ-map {↓↓} f .rel⇒ = f .rel⇒
    !ᴿ-map {??} f .rel⇒ = _
    !ᴿ-map {~~} f .rel⇒ = ×.map (f .rel⇒) (f .rel⇒)

    !ᴿ-≤ : ∀ {a b} {R : WRel _≤ʷ_} → a ≤ b → WRelMor (!ᴿ a R) (!ᴿ b R)
    !ᴿ-≤ ab .set⇒ = id
    !ᴿ-≤ ≤-refl .rel⇒ r = r
    !ᴿ-≤ ≡≤↑ .rel⇒ (r , s) = r
    !ᴿ-≤ ≡≤↓ .rel⇒ (r , s) = s
    !ᴿ-≤ ↑≤? .rel⇒ r = _
    !ᴿ-≤ ↓≤? .rel⇒ r = _
    !ᴿ-≤ ≡≤? .rel⇒ (r , s) = _

    !ᴿ-0 : ∀ {a R} → a ≤ ?? → WRelMor (!ᴿ a R) Iᴿ
    !ᴿ-0 = _

    !ᴿ-+ : ∀ {a b c R} → a ≤ b ∧ c → WRelMor (!ᴿ a R) (!ᴿ b R ⊗ᴿ !ᴿ c R)
    !ᴿ-+ le .set⇒ x = x , x
    !ᴿ-+ {a} {↑↑} {↑↑} le .rel⇒ r = !ᴿ-≤ le .rel⇒ r ✴⟨ _ ⟩ !ᴿ-≤ le .rel⇒ r
    !ᴿ-+ {a} {↑↑} {↓↓} le .rel⇒ r =
      !ᴿ-≤ le .rel⇒ r .proj₁ ✴⟨ _ ⟩ !ᴿ-≤ le .rel⇒ r .proj₂
    !ᴿ-+ {a} {↑↑} {??} le .rel⇒ r = !ᴿ-≤ le .rel⇒ r ✴⟨ _ ⟩ _
    !ᴿ-+ {.(↑↑ ∧ ~~)} {↑↑} {~~} ≤-refl .rel⇒ (r , s) = r ✴⟨ _ ⟩ (r , s)
    !ᴿ-+ {a} {↓↓} {↑↑} le .rel⇒ r =
      !ᴿ-≤ le .rel⇒ r .proj₂ ✴⟨ _ ⟩ !ᴿ-≤ le .rel⇒ r .proj₁
    !ᴿ-+ {a} {↓↓} {↓↓} le .rel⇒ r = !ᴿ-≤ le .rel⇒ r ✴⟨ _ ⟩ !ᴿ-≤ le .rel⇒ r
    !ᴿ-+ {a} {↓↓} {??} le .rel⇒ r = !ᴿ-≤ le .rel⇒ r ✴⟨ _ ⟩ _
    !ᴿ-+ {.(↓↓ ∧ ~~)} {↓↓} {~~} ≤-refl .rel⇒ (r , s) = s ✴⟨ _ ⟩ (r , s)
    !ᴿ-+ {a} {??} {c} le .rel⇒ r = _ ✴⟨ _ ⟩ !ᴿ-≤ le .rel⇒ r
    !ᴿ-+ {.(~~ ∧ c)} {~~} {c} ≤-refl .rel⇒ r = r ✴⟨ _ ⟩ !ᴿ-≤ (~~≤ {c}) .rel⇒ r

    !ᴿ-1 : ∀ {a R} → a ≤ ↑↑ → WRelMor (!ᴿ a R) R
    !ᴿ-1 le .set⇒ = id
    !ᴿ-1 {.↑↑} ≤-refl .rel⇒ r = r
    !ᴿ-1 {.~~} ≡≤↑ .rel⇒ r = r .proj₁

    !ᴿ-* : ∀ {a b c R} → a ≤ b * c → WRelMor (!ᴿ a R) (!ᴿ b (!ᴿ c R))
    !ᴿ-* le .WithPoSemiring.set⇒ = id
    !ᴿ-* {a} {↑↑} {c} le .WithPoSemiring.rel⇒ r = !ᴿ-≤ le .rel⇒ r
    !ᴿ-* {a} {↓↓} {↑↑} le .WithPoSemiring.rel⇒ r = !ᴿ-≤ le .rel⇒ r
    !ᴿ-* {a} {↓↓} {↓↓} le .WithPoSemiring.rel⇒ r = !ᴿ-≤ le .rel⇒ r
    !ᴿ-* {a} {↓↓} {??} le .WithPoSemiring.rel⇒ r = _
    !ᴿ-* {a} {↓↓} {~~} le .WithPoSemiring.rel⇒ r = ×.swap (!ᴿ-≤ le .rel⇒ r)
    !ᴿ-* {a} {??} {c} le .WithPoSemiring.rel⇒ r = _
    !ᴿ-* {.(~~ * ↑↑)} {~~} {↑↑} ≤-refl .WithPoSemiring.rel⇒ r = r
    !ᴿ-* {.(~~ * ↓↓)} {~~} {↓↓} ≤-refl .WithPoSemiring.rel⇒ r = ×.swap r
    !ᴿ-* {a} {~~} {??} le .WithPoSemiring.rel⇒ r = _
    !ᴿ-* {.(~~ * ~~)} {~~} {~~} ≤-refl .WithPoSemiring.rel⇒ r = r , ×.swap r

    !ᴿ-I : ∀ {a} → WRelMor Iᴿ (!ᴿ a Iᴿ)
    !ᴿ-I .set⇒ = id
    !ᴿ-I {↑↑} .rel⇒ = _
    !ᴿ-I {↓↓} .rel⇒ = _
    !ᴿ-I {??} .rel⇒ = _
    !ᴿ-I {~~} .rel⇒ = _

    !ᴿ-⊗ : ∀ {a R S} → WRelMor (!ᴿ a R ⊗ᴿ !ᴿ a S) (!ᴿ a (R ⊗ᴿ S))
    !ᴿ-⊗ .set⇒ = id
    !ᴿ-⊗ {↑↑} .rel⇒ r = r
    !ᴿ-⊗ {↓↓} .rel⇒ r = r
    !ᴿ-⊗ {??} .rel⇒ _ = _
    !ᴿ-⊗ {~~} .rel⇒ ((r , s) ✴⟨ _ ⟩ (r′ , s′)) = (r ✴⟨ _ ⟩ r′) , (s ✴⟨ _ ⟩ s′)

    bang : Bang
    bang = record
      { !ᴿ = !ᴿ
      ; !ᴿ-map = !ᴿ-map
      ; !ᴿ-0 = !ᴿ-0
      ; !ᴿ-+ = !ᴿ-+
      ; !ᴿ-1 = !ᴿ-1
      ; !ᴿ-* = !ᴿ-*
      ; !ᴿ-I = !ᴿ-I
      ; !ᴿ-⊗ = !ᴿ-⊗
      }

    open WithSyntax poSemiring ⊤ public
    open WithSemantics 0CRM (λ _ → Baseᴿ) bang public

  open import Data.LTree.Automation
  open import Data.Nat using (ℕ)

  open import Data.Integer as ℤ
  open import Data.Integer.Properties
  open WithPoset ≤-poset

  open import Generic.Linear.Example.UsageCheck Ty as UC
  open UC.WithPoSemiring poSemiring
  open WithInverses record
    { 0#⁻¹ = ??⁻¹ ; +⁻¹ = ∧⁻¹ ; 1#⁻¹ = ↑↑⁻¹ ; *⁻¹ = *⁻¹ }
\end{code}

%<*patterns>
\begin{code}
  pattern ulam M = U.`con (`lam _ _ , ≡.refl , M)
  pattern uapp M N = U.`con (`app _ _ , ≡.refl , M ✴⟨ _ ⟩ (⟨ _ ⟩· N))
\end{code}
%</patterns>

%<*minus>
\begin{code}
  minus : [ AnnArr , ∞ ]
    [ ~~ , (↑↑ , base tt) ⊸ (↑↑ , base tt) ⊸ base tt ]ᶜ ++ᶜ
    [ ~~ , (↓↓ , base tt) ⊸ base tt ]ᶜ
    ⊢ ((↑↑ , base tt) ⊸ (↓↓ , base tt) ⊸ base tt)
  minus =
    let #0 = Ptr ((((([-] <+> [-]) <+> [-]) <+> [-]) <+> ε) <+> ε) F.∋ # 0 in
    elab-unique AnnArr
    (ulam (ulam (uapp (uapp (uvar (#0)) (uvar (# 2))) (uapp (uvar (# 1)) (uvar (# 3))))))
    ([ ~~ ] ++ [ ~~ ])
\end{code}
%</minus>

%<*minus-set>
\begin{code}
  minus-set⇒ : wrel minus .set⇒ (ℤ._+_ , ℤ.-_) ≡ λ x y → x ℤ.+ (ℤ.- y)
  minus-set⇒ = ≡.refl
\end{code}
%</minus-set>

%<*thm>
\begin{code}
  private
    variable
      x x′ y y′ : ℤ

  minus-mono = wrel minus .rel⇒ $
    ((λ where .app✴ _ xx .app✴ _ yy → +-mono-≤ xx yy) ,
     (λ where .app✴ _ xx .app✴ _ yy → +-mono-≤ xx yy))
      ✴⟨ _ ⟩
    ((λ where .app✴ _ xx → neg-mono-≤ xx) ,
     (λ where .app✴ _ xx → neg-mono-≤ xx))
\end{code}

%<*thm-type>
\begin{code}
  thm : x ℤ.≤ x′ → y′ ℤ.≤ y → x ℤ.+ (ℤ.- y) ℤ.≤ x′ ℤ.+ (ℤ.- y′)
\end{code}
%</thm-type>
\begin{code}
  thm xx yy = minus-mono .app✴ _ xx .app✴ _ yy
\end{code}
%</thm>
